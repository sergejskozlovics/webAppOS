  apply plugin: 'java'
  apply plugin: 'application'

  repositories {
      jcenter()
      mavenCentral()

  // Eclipse OCL
      ivy {
        url "https://download.eclipse.org/modeling/mdt/ocl/updates/releases/6.6.0/plugins"
        patternLayout {
              artifact "[artifact]_[revision].[ext]"
        }
      }
  // Eclipse Acceleo
      ivy {
        url "https://download.eclipse.org/acceleo/updates/releases/3.7/R201812041426/plugins"
        patternLayout {
              artifact "[artifact]_[revision].[ext]"
        }
      }
  }

  dependencies {
      implementation group: 'org.eclipse.platform', name: 'org.eclipse.core.runtime', version: '3.15.200'
      implementation group: 'org.eclipse.platform', name: 'org.eclipse.core.contenttype', version: '3.7.300'
      implementation group: 'org.eclipse.platform', name: 'org.eclipse.osgi', version: '3.13.300'
      implementation group: 'org.eclipse.platform', name: 'org.eclipse.equinox.common', version: '3.10.300'
      implementation group: 'org.eclipse.platform', name: 'org.eclipse.equinox.registry', version: '3.8.300'

      implementation group: 'org.eclipse.emf', name: 'org.eclipse.emf.common', version: '2.15.0'
      implementation group: 'org.eclipse.emf', name: 'org.eclipse.emf.ecore', version: '2.15.0'
      implementation group: 'org.eclipse.emf', name: 'org.eclipse.emf.ecore.xmi', version: '2.15.0'

      implementation 'com.google.guava:guava:[26.0,)'
      implementation 'lpg.runtime:java:2.0.17-v201004271640'

      implementation 'org.eclipse.ocl:org.eclipse.ocl.ecore:3.10.100.v20181210-1441'
      implementation 'org.eclipse.ocl:org.eclipse.ocl.common:1.8.100.v20181210-1441'
      implementation 'org.eclipse.ocl:org.eclipse.ocl:3.10.100.v20181210-1441'

      implementation 'org.eclipse.acceleo:org.eclipse.acceleo.engine:3.7.7.201812041426'
      implementation 'org.eclipse.acceleo:org.eclipse.acceleo.common:3.7.7.201812041426'
      implementation 'org.eclipse.acceleo:org.eclipse.acceleo.model:3.7.7.201812041426'
      implementation 'org.eclipse.acceleo:org.eclipse.acceleo.profiler:3.7.7.201812041426'
  }

  // Define the main class for the application
  mainClassName = 'ecore2java.main.Generate'

  distZip.enabled = false
  distTar.enabled = false
  jar.enabled = true

  installDist {
    into('../../../dist')
    preserve({
          include '**'
          exclude 'bin/ecore2java*'
          exclude 'lib/ecore2java.jar'
    })
  }

